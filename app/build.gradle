plugins {
  id "org.sonarqube" version "3.3"
  id 'com.android.application'
}

final SONAR_PROJECT_VERSION = '1.58'

android {
  namespace 'com.example.myapplication'
  compileSdk 34

  defaultConfig {
    applicationId "fr.ralala.hexviewer"
    minSdk 23
    targetSdk 34
    // F-Droid seems not to appreciate the use of variables
    versionCode 158
    versionName '1.58'
    vectorDrawables {
      useSupportLibrary true
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      // to debug ProGuard rules
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
    }
  }
  buildFeatures {
    compose true
  }
  // This is important, it will run lint checks but won't abort build
  lintOptions {
    abortOnError false
  }
  sonarqube {
    properties {
      property "sonar.projectKey", "Keidan_HexViewer"
      property "sonar.organization", "keidan"
      property "sonar.host.url", "https://sonarcloud.io"
      property 'sonar.projectName', 'HexViewer'
      property 'sonar.projectVersion', SONAR_PROJECT_VERSION
      property 'sonar.coverage.exclusions', "**/**"
      // Exclude rules
      property "sonar.issue.ignore.multicriteria", "e1,e2,e3,e4,e5,e6,e7,e8,e9,e10,e11,e12,e13,e14,e15"
      /* S110 */
      property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S110"
      property "sonar.issue.ignore.multicriteria.e1.resourceKey", "**/SettingsActivity.java"
      property "sonar.issue.ignore.multicriteria.e2.ruleKey", "java:S110"
      property "sonar.issue.ignore.multicriteria.e2.resourceKey", "**/SettingsListsLandscapeActivity.java"
      property "sonar.issue.ignore.multicriteria.e3.ruleKey", "java:S110"
      property "sonar.issue.ignore.multicriteria.e3.resourceKey", "**/SettingsListsPortraitActivity.java"
      /* S7091 */
      property "sonar.issue.ignore.multicriteria.e4.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e4.resourceKey", "**/ApplicationCtx.java"
      property "sonar.issue.ignore.multicriteria.e5.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e5.resourceKey", "**/MainActivity.java"
      property "sonar.issue.ignore.multicriteria.e6.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e6.resourceKey", "**/SettingsActivity.java"
      property "sonar.issue.ignore.multicriteria.e7.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e7.resourceKey", "**/SettingsFragment.java"
      property "sonar.issue.ignore.multicriteria.e8.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e8.resourceKey", "**/ListSettings.java"
      property "sonar.issue.ignore.multicriteria.e9.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e9.resourceKey", "**/PartialOpenActivity.java"
      property "sonar.issue.ignore.multicriteria.e10.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e10.resourceKey", "**/LauncherLineUpdate.java"
      property "sonar.issue.ignore.multicriteria.e11.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e11.resourceKey", "**/UnDoRedo.java"
      property "sonar.issue.ignore.multicriteria.e12.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e12.resourceKey", "**/DeleteCommand.java"
      property "sonar.issue.ignore.multicriteria.e13.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e13.resourceKey", "**/PayloadHexHelper.java"
      property "sonar.issue.ignore.multicriteria.e14.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e14.resourceKey", "**/HexMultiChoiceCallback.java"
      property "sonar.issue.ignore.multicriteria.e15.ruleKey", "java:S7091"
      property "sonar.issue.ignore.multicriteria.e15.resourceKey", "**/LauncherLineUpdate.java"
    }
  }
  namespace 'fr.ralala.hexviewer'
}

dependencies {
  implementation 'org.apache.commons:commons-collections4:4.0'
  implementation "androidx.appcompat:appcompat:1.6.1"
  implementation "androidx.preference:preference:1.2.1"
  implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
  implementation "androidx.emoji:emoji:1.1.0"
  implementation "com.android.support:support-emoji-bundled:$version"
  implementation "com.google.android.material:material:1.11.0"
  testImplementation "junit:junit:4.13.2"
}
// versionCode <-> versionName /////////////////////////////////////////////////////////////////////
// I got the idea (code) below from the repository https://github.com/sal0max/currencie
/**
 * Checks if versionCode and versionName match.
 * Needed because of F-Droid: both have to be hard-coded and can't be assigned dynamically.
 * So at least check during build for them to match.
 */
tasks.register('checkVersion') {
  int versionCode = android.defaultConfig.versionCode
  String versionName = android.defaultConfig.versionName
  int correctVersionCode = generateVersionCode(android.defaultConfig.versionName)
  if (versionCode != correctVersionCode)
    throw new GradleException(
      "versionCode and versionName don't match: " +
        "versionCode should be $correctVersionCode. Is $versionCode."
    )
  if (versionName != SONAR_PROJECT_VERSION)
    throw new GradleException(
      "versionName and sonar.projectVersion don't match: " +
        "sonar.projectVersion should be $versionName. Is $SONAR_PROJECT_VERSION."
    )
}
assemble.dependsOn checkVersion

/**
 * Checks if a fastlane changelog for the current version is present.
 */
tasks.register('checkFastlaneChangelog') {
  int versionCode = android.defaultConfig.versionCode
  File changelogFile = file("$rootDir/fastlane/metadata/android/en-US/changelogs/${versionCode}.txt")
  if (!changelogFile.exists())
    throw new GradleException(
      "Fastlane changelog missing: expecting file '$changelogFile'"
    )
}
build.dependsOn checkFastlaneChangelog

/**
 * Generates a versionCode based on the given semVer String.
 * See e.g. https://proandroiddev.com/keep-salinity-with-your-versioncode-db2089b640b9
 *
 * @param semVer e.g. 1.27
 * @return e.g. 127 (-> 1 27)
 */
private static int generateVersionCode(String semVer) {
  return semVer.split('\\.')
    .collect { Integer.parseInt(it) }
    .inject { sum, value -> sum * 100 + value }
}